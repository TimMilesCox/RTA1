							; static void oport(int ea, int value, smp *xcore)
							; {
oport:		; write I / O port			;    int		 rule,
		; RTA instructions outA outB		;			 device;
							; 
		; ports which are memory pointers	;    int		 mask,
		; must have a block that exists		;			 from,
		; ISRs can write trap value C00001 	;                 	 index;
		; = device 400001 | illegal 800000	; 
		; port # = effective address = eax	;    int		 device_index = 0;
		; write value in ebx			; 
		; multicast bits in RTA register k	;    smp		*ascor;
		 					;    unsigned	*register_set = xcore->register_set;
	cmp	eax, IO_PORTS				; 
	jnb	no_port					;    if (ea < IO_PORTS)
							;    {
	mov	cl, byte ptr [eax+mask_port_outAB]	;       rule = mask_port_outAB[ea];
	and	cl, cl					; 
	jz	no_rule					;       if (rule)
	test	cl, ISR_ONLY				;       {
	jz	application_plus_isr_rule		;          if (((rule & ISR_ONLY) == 0) || (psr & 0x00800000))
	test	byte ptr [_psr+2], 128			;          {
	jz	no_authority				;
							;
application_plus_isr_rule:				;
	test	cl, EXTERNAL_IO				;             if (rule & EXTERNAL_IO)
	jz	broadcast_rule?				;             {
	mov	dword ptr [eax*4+_base], ebx		;                base[ea] = value;
	cmp	eax, 102	; INDICATION$		;                if (ea == 102) indication |= value;
	jne	xi_ack?		 			;
	or	dword ptr [_indication], ebx		;
 							;
xi_ack?:							;
	cmp	eax, 75					;                if (ea == 75)
	jne	xi_no_ack				;                {
	not	bl					;                    base[74] &= (value & 255) ^ 255;
	movzx	ebx, bl					;                    // indication |= (value & 255) << 8;
							;                }
	and	dword ptr [_base+74*4], ebx		; 
xi_no_ack:						; //               if ((ea == 79) && (value & 1)) indication |= ATTENTION;
	ret						;                return;
							;             }
							; 
broadcast_rule?:					;             if (rule & BROADCAST)
	test	cl, BROADCAST				;             {
	jz	memory_rule?				;                mask = k;
	test	byte ptr [ebp+K+1], 1			;                from = base[77];
	; there is only core 0				; 
	jz	storecast				;                for (index = 0; index < RTA_SMP; index++)
;	 write the port still means write the port	;                {
	or	byte ptr [_base+78*4+1], 1		;                   if (mask & 1)
	or	byte ptr [_indication], ATTENTION	;                   {
							;                      ascor = core + index;
storecast:						;                      ascor->BASE[78] |= 1 << from;
	mov	dword ptr [eax*4+_base], ebx		;                      ascor->INDICATION |= ATTENTION;
	ret						;                   }
							; 
							;                   mask >>= 1;
							;                }
no_port:						; 
no_rule:						;                /**************************************************
no_authority:						; 			keep these on the sending side
	mov	esi, LP_AUTHORITY			; 			until constructing interrupt frame
	jmp	guard_ii_escape				; 			between instructions at target core
							;                         to avoid overwrites from other cores
							;                **************************************************/
							; 
							;                base[ea] = value;	/* latent parameter	*/
							;                return;
							;             }
							; 
memory_rule?:	; system memory type only		;             else if (rule & MEMTYPE)
	test	cl, MEMTYPE				;             {
	jz	page_pointer?				;                if (value & 0x00400000) device_index = value & 63;
	test	ebx, 0400000h	; bank sized?		; 
	jz	memory_page_pointer			;                if (device_index)
	mov	esi, ebx				;                {
	and	esi, 63					;                   if (value & 0x00800000)
	jz	device_is_pointed			;                   {
	test	ebx, 0800000h	; the trap device?	;                      if (psr & 0x00800000)
	jz	device_is_pointed			;                      {
	test	byte ptr [_psr+2], 128	; ISR?		;                         base[ea] = value;
	jz	no_authority				;                         return;
	mov	[eax*4+_base], ebx			;                      }
	ret						; 
							;                      GUARD_AUTHORITY
device_is_pointed:					;                      return;
	mov	ecx, dword ptr [esi*4+128*4+_base]	;                   }
	test	ecx, 00800000h				;                }
	jz	no_authority				; 
	test	ecx, 00400000h				;                device = base[128+device_index];
	jnz	no_authority				;            
	and	ecx, 003FFFFFh				;                if ((device & 0x00C00000) == 0x00800000)
	mov	esi, ebx				;                {
	and	esi, 00BFFFFFh	; catch any 00800000	;                   if ((value & 0x003FFFC0) > (device & 0x003FFFFF))
	cmp	ecx, esi				;                   {
	jb	outwith		; beyond memory		;                   }
	mov	[eax*4+_base], ebx			;                   else
	ret						;                   {
							;                      base[ea] = value;
							;                      return;
							;                   }
							;                }
							;             }
							;             else
							;             {
							;                /*******************************************
;	only a device zero page index will do		; 			page pointer to system memory
							;                *******************************************/
page_pointer?:						; 
	test	cl, MEMZERONLY				;                if ((value & 0x00400000) == 0)
	jz	no_rule					;                {
	test	ebx, 00C00000h				;                   if (value > (base[128] & 0x003FFFFF))
	jnz	no_authority				;                   {
							;                   }
memory_page_pointer:					;                   else
	mov	ecx, dword ptr [_base+128*4]		;                   {
	and	ecx, 003FFFFFh				;                      base[ea] = value;
	and	ebx, 00FFFFFFh				;                      return;
	cmp	ecx, ebx				;                   }
	jb	outwith					;                }
	mov	[eax*4+_base], ebx			;             }
	ret						;          }
							;       }
							;    }
outwith:							; 
	mov	esi, LP_ADDRESS				;    GUARD_AUTHORITY;
	jmp	guard_ii_escape				; }							; 
