

;	include	rta_x86.h
;	.model	large
;	.code

;	this is the operand reader / writer
;	for emulated RTA1 on x86

;	layout is

;	RTA1 24-bit registers are modeled in an array of
;	platform-endian integers. In x86 these are 32-bit
;	little-endian integers

;	platform-endian doesn't affect registers
;	because endianness is not at the CPU
;	side of the bus, but at the memory side

;	when Intel circuitry in the x86 CPU staticises the value
;	of an emulated register, that value is big endian

;	the stored integers where emulated registers are modeled
;	are platform-endian, with identical effect on big and
;	little endian emulator platforms

;	RTA1 24-bit executable space storage words are modeled in
;	another array of platform-endian 32-bit integers. Emulation
;	on x86 swaps the byte order of storage words on read and
;	before write to conform with RTA1 canonical storage order

;       RTA1 architecture has registers in the first 256 locations
;       of address space and access differentiates registers from
;       from memory according to effective address

;	location of registers and storage are handed to read / write
;	routines as RTA1 word linear effective addresses

;	These routines maintain the RTA1 linear addresses passed
;	in x86 registers and apply them with shifted scale plus
;	displacement, for example

;		mov	eax, dword ptr [edi+esi*4+_memory]
;		bswap	eax
;		mov	dword_ptr [ebp+ebx*4], eax

;	RTA1 register addresses are already absolute in terms of
;	the emulated RTA1 machine

;	ebp points at the context register frame. There are two
;	register frames, application and interrupt. Registers
;	addressed as storage operand are in a single list

;		mov	eax, dword ptr [esi*4+__register]

;	RTA1 storage addresses are translated by RTA1 relocation
;	architecture before application. The updated linear addresses
;	of the emulated machine are in esi

;	The operand routines in this file point edi to the x86 platform
;	storage array start of the storage device, most often emulated
;	RTA1 executable space

;	instructions call these operand routines with RTA1 effective 
;	address in eax

;	instructions receive load operand values in eax and ebx, or in
;	a list of RTA1 emulated register locations pointed by ebp+ebx

;	store instructions submit a value in ebx, or values in list of
;	RTA1 emulated register locations pointed by ebp+ebx

;	to preserve linear address consistency, application instruction
;	work areas, where needed in addition to x86 registers, are
;	implemented as sub-architectural extra RTA1 registers at the end
;	of the 256-longword register stack. x86 register ebp+ebx*4 identify
;	these locations by linear RTA1 word quantum as "registers"

;	RTA1 registers and storage alike are in 24-bit word address quantum space

;	emulated instructions loading or storing multiple hidden extra work
;	registers should unconditionally set ebp to the start or application
;	frame of the emulated register array, so that [ebp+ebx*4] accesses the
;	correct locations at register occurence 256+

;               _floating_operand2 equ  256+12

;		push	ebp
;		mov	ebp, __register	; point to application register frame[0]
;		mov	ebx, 256+_floating_operand2
;		call	_burst_read4

;		pop	ebp		; return to previous register frame[0] application
					;                          or frame[1] interrupt

;	Both internal stack pointers are also absolute, so for stack operations
;	ebp should be momentarily forced to the lower-addressed (application) stack
;	frame after being used to point the stack location of reference

;		mov	ebx, dword ptr [ebp+S_P]	; read one of the two stack pointers
;		cmp	ebx, ebp+STACK_LOWER_LIMIT	; 24*4 within context of either stack
;		jl	II_GUARD
;		add	ebx, -1				; new stack top
;		mov	dword ptr [ebp+S_P], ebx
;		push	ebp				; which ever stack pointer it is, it points
;		mov	ebp,__register			; relative to all the register stack
;		call	_operand_read			; for I am the single-word push instruction
;		pop	ebp				;

;	register identities supplied from emulated instructions to these routines
;	are their lowercase names equated to RTA1 word addresses

;	There exist also uppercase spellings which instruction routines may
;	access internally. The uppercase names are four times in value the
;	equivalent lowercase ordinal names to give x86 platform offset values

;       Assembly language equate is like parentheses! What's in a label
;       is not an expression but its value. Nothing like C preprocessor.
;       Just in case you're developing enhancements in assembly using
;       preprocessor and #define

;		_floating_operand2 equ	256+12
;		FLOATING_OPERAND2 equ	_floating_operand2*4


;		a	equ	4
;		b	equ	5
;		mantissa2 equ	6

;		A	equ	a*4
;		B	equ	b*4
;		MANTISSA2 equ	mantissa2*4

;		mov	ebx, dword ptr [bp+MANTISSA2]	; the very same!
;		mov	ebx, dword ptr [bp+mantissa2*4]	; the very same

;	but to identify an instruction-implied register to these operand routines

;		mov	ebx, mantissa2		; bp already says which register set

;	instruction evaluation supplies right-hand side operand EA in eax
;	Instruction implementation needs not examine EA. Call _operand_read
;	or _operand_write. eax must be as supplied to the instruction routine
;	on on operand retrieve and store

;	simple instructions receive operand mode in cl from instruction
;	evaluation

;	shifts and jumps use the ea which is already indexed or indirected
;	if necessary and make no operand call

;	instructions call _burst_read2 or _burst_write2 for two-word operand cycles
;	instructions call _burst_read4 or _burst_write4 for four-word operand cycles

;	operand reads of 1 or 2 words return values in eax:ebx

;		call	_burst_read2
;		mov	dword ptr [ebp+A], eax
;		mov	dword ptr [ebp+B], ebx

;	to store one operand word place it in ebx

;	_sc	mov	ebx, dword ptr[_psr]	; I am the store carry instruction sc
;		and	ebx, 1			; CARRY
;		call	_operand_write

;	to store two or four words or to load four words, point bx to register

;	_qpop	mov	ebx, dword ptr [ebp+S_P] ; internal stack pointer
;						; either application sp or interrupt sp
;
;		add	dword ptr [ebp+S_P], 4	; internal stack+= 4
;		push	ebp			;
;		mov	ebp, __register		; in case context is interrupt registers
;		call	_burst_write4		; both internal stack pointers are absolute
;		pop	ebp			; point epb back to application registers
;						;                or interrupt registers

;	more complex instructions do something to the registers after
;	acquiring operands

;		call	_operand_read
;		add	eax, dword ptr [ebp+A]
;		rol	eax
;		and	al, CARRY		; 1
;		or	byte ptr [_psr], al	; low-order byte of longword _psr
;		shr	eax, 8
;		mov	dword ptr [ebp+A], eax

;	emulated RTA1 instructions which both read and write (modify)
;	storage use the address in [edi+esi*4] to rewrite. edi is
;	NULL if it's not a suitable memory for a modify operation

;	modify instructions which may have a register operand (not TS)
;	call _operand_write to rewrite a register. This guards aganst
;	applications writing interrupt registers.
 
;	the effect of storage buses characteristically different
;	from RTA1 executable space is emulated in the routines
;	device_read and device_write

;	Peripheral bus types implemented are

;		filestore containing extents of up to 768K octets
;		accessible to RTA1 as up to 256K words in each extent.
;		The 3-octet words are in canonical order and packed

;		network interface buffer store shared with the emulator
;		platform and reading out to RTA1 at 16 data bits per word
;		with eight high-order zero bits. RTA writes 16 data bits
;		to these locations from register low-order bit positions.
;		Storage byte order is canonical. These buffers are normal
;		and contiguous byte arrays of the emulator platform.
;		To RTA1 they are an attached device array of 16-bit words

;	Device array types are

;		executable space
;		24-bit filestore array
;		16-bit network interface array

;	devices are identified in RTA1 memory relocation pointers and
;	tagged with device type. Executable space is device zero

;	esi -> core.REGISTER

_burst_read2:
	mov	cx, 6
	test	eax, 00FFFF00h
	jz	read2_registers
	jmp	_bus_read

read2_registers:				; load registers from registers
	%if	__REGFOR

	mov	rax, qword ptr [rsi+rax*4]	; swap the halves of r8 if computing in r8
	ret					; just mov for double register load
						; do this load inline anyway
	%else	

	mov	ebx, dword ptr [rsi+rax*4+4]
	mov	eax, dword ptr [rsi+rax*4]
	ret

	%endif
	
;	rsi -> core.REGISTER

_burst_read4:					; buffered read
	mov	cx, 7				; that will help the caller
	test	eax, 00FFFF00h			; ebp+ebx contains a register number
	jz	read4_registers			; may also identify extra
	jmp	_bus_read
						; workspace registers
read4_registers:				; at register stack tail

	%if	__REGFOR			; registers are modelled
						; platform endian in storage
	mov	rax, qword ptr [rsi+rax*4+2*4]	; core-ways round individually but reverse order
	mov	rdx, qword ptr [rsi+rax*4]	; into correct register order individually platform-ordered 
	ret

	%else

	mov	ecx, dword ptr [rsi+rax*4+3*4]
	mov	dword ptr [rbp+rbx*4+3*4], ecx
	mov	ecx, dword ptr [rsi+rax*4+2*4]
	mov	dword ptr [rbp+rbx*4+2*4], ecx
	mov	ecx, dword ptr [rsi+rax*4+4]
	mov	dword ptr [rbp+rbx*4+4], ecx
	mov	ecx, dword ptr [rsi+rax*4]
	mov	dword ptr [rbp+rbx*4], ecx
	ret

	%endif

;	esi -> core.REGISTER

_operand_read:			; call here with designator coded in cx
	and	cl, 7		; and ea in eax
	cmp	cl, 4
	je	immediate	; EA is operand
	cmp	cl, 5
	je	immediate_xi	; EA is operand

	test	eax, 00FFFF00h
	jnz	_bus_read	; ea does not point to registers
	mov	eax, dword ptr [rsi+rax*4]

immediate:
immediate_xi:
	ret 

_bus_read:				; readout any memory

	call	base_read_pointer	; returns eax <- storage block
					; 	  esi <- word offset
					;	  edi <- EA

	test	eax, 00400000h		; big block?
	je	memory_read_4k_block

	cmp	cl, 6			; multiword operation?
	jb	_bus_read_256k_block

	cmp	esi, 03FFFCh		; near the edge?
	jna	_bus_read_256k_block	; no

	call	read_straddle_18b?

_bus_read_256k_block:
	and	eax, 003FFFFFh		; 64-bit shift to page won't brush off 00400000 bit
	test	al, 63
	jz	memory_read_page

	jmp	device_read

memory_read_4k_block:
	test	esi, 0003F000h		; inside 4k block?
	jz	memory_read_thru

	mov	ecx, LP_ADDRESS
	jmp	guard_ii_escape		; no

memory_read_thru:
	cmp	cl, 6
	jb	memory_read_page

	cmp	rsi, 0FFCh		; near the edge?
	jna	memory_read_page	; no

	call	read_straddle_12b?

memory_read_page:
	shl	rax, 12			; multiply by page and lose bit 00400000
	add	rsi, rax
;	mov	ch, byte ptr [edi+PSR]	; field of PSR with byte options
	mov	rdi, _memory		; byte address

memory_read_on:
	lea	rdi, [rdi+rsi*4]

	movq	rsi, mm7
;	mov	rsi, qword ptr [rbp+CORE_INDEX1]
	movzx	rax, cl
	mov	rax, qword ptr [read_action+rax*8]
	jmp	rax

read_action:
	dq	memory_read_w0
	dq	memory_t1
	dq	memory_t2	; or h1
	dq	memory_t3	; or h2
	dq	immediate	; don't get here
	dq	immediate_xi	; don't get here
	dq	memory_read2
	dq	memory_read4

memory_read_w0:			; readout system memory 1 word

	%if	__MOVBE
	movbe	eax, dword ptr [rdi]
	%else
	mov	eax, dword ptr [rdi]
	mov	al, 0
	bswap	eax
	%endif
; immediate:
; immediate_xi:
 	ret

memory_read2:			; readout system memory 2 words
	%if	__MOVBE

	movbe	rax, qword ptr [rdi]
	ret

	%else

	mov	ebx, dword ptr [rdi+4]
	mov	eax, dword ptr [rdi]

	mov	al, 0
	mov	bl, 0
	bswap	eax
	bswap	ebx
	ret

	%endif
				; readout system memory 4 words
memory_read4:			; store by pointer
				; this will be useful to the caller

	%if	__MOVBE
	
	movbe	rax, qword ptr [rdi+2*4]
	movbe	rdx, qword ptr [rdi]
	ret

	%else

	mov	eax, dword ptr [rdi]
	mov	al, 0
	bswap	eax
	mov	dword ptr [rbp+rbx*4], eax
	mov	eax, dword ptr [rdi+4]
	mov	al, 0
	bswap	eax
	mov	dword ptr [rbp+rbx*4+4], eax
	mov	eax, dword ptr [rdi+2*4]
	mov	al, 0
	bswap	eax
	mov	dword ptr [rbp+rbx*4+2*4], eax
	mov	eax, dword ptr [rdi+3*4]
	mov	al, 0
	bswap	eax
	mov	dword ptr [rbp+rbx*4+3*4], eax
	ret

	%endif

memory_t1:
	mov	eax, dword ptr [rdi]
	shl	eax, 16		; t1 = 3.2.THIS.zero
	jmp	memory_tw

memory_t2:
	mov	eax, dword ptr [rdi]
	test	byte ptr [rsi+PSR], HALF_W
	jnz	memory_h1
	shl	eax, 8		; t2 = 3.THIS.1.zero
	jmp	memory_tw

memory_t3:
	mov	eax, dword ptr [rdi]
	test	byte ptr [rsi+PSR], HALF_W
	jnz	memory_h2
memory_tw:
	sar	eax, 16		; t3 = THIS.2.1.zero
	shr	eax, 8
	ret

memory_h1:
	bswap	eax		; already loaded but not swapped
	shl	eax, 8		; h1 = zero.HERE.2.3
	jmp	memory_hw

memory_h2:
	bswap	eax		; already loaded but not swapped
	shl	eax, 8+12
memory_hw:
	sar	eax, 12
	shr	eax, 8
	ret

;	esi -> core.REGISTER
				; store system memory
_burst_write4:
	mov	cx, 7
	test	eax, 00FFFF00h
	jnz	NEAR bus_write

	; store emulated registers to emulated registers

	test	dword ptr [rsi+PSR], 0800000h	; ISR ?
	jnz	write4_registers	; ISRs may write some spare registers
	cmp	eax, 124
	jna	write4_registers
	jmp	guard_ii_authority	; but applications may not write
					; any interrupt registers

write4_registers:
;	mov	edi, eax		; eax can be freed

	%if	__REGFOR
	mov	rdx, qword ptr [rbp+rbx*4]
	ror	rdx, 32
	mov	qword ptr [rsi+REGISTER+rax*4], rdx

	mov	rdx, qword ptr [rbp+rbx*4+2*4]
	ror	rdx, 32
	mov	qword ptr [rsi+REGISTERrax*4+2*4], rdx
	ret

	%else

	mov	ecx, dword ptr [rbp+rbx*4]	; data for store
					; ebp+ebx is a register number
					; which can be extra register locations
					; serving as workspace at the tail of
					; the register stack

	mov	dword ptr [rsi+rax*4], ecx
	mov	ecx, dword ptr [rbp+rbx*4+4]
	mov	dword ptr [rsi+rax*4+4], ecx

	mov	ecx, dword ptr [rbp+rbx*4+2*4]
	mov	dword ptr [rsi+rax*4+2*4], ecx
	mov	ecx, dword ptr [rbp+rbx*4+3*4]
	mov	dword ptr [rsi+rax*4+3*4], ecx
	ret

	%endif

;	esi -> core.REGISTER

_burst_write2:
	mov	cx, 6
	test	eax, 00FFFF00h
	jnz	bus_write

	; store to emulated registers

	test	dword ptr [rsi+PSR], 0800000h	; ISR ?
	jnz	write2_registers	; there are spare registers at the end
	cmp	eax, 126		; but applications may not write
	jna	write2_registers	; any interrupt registers
	jmp	guard_ii_authority

write2_registers:
;	mov	edi, eax			; time to free eax

	%if	__REGFOR

	mov	rdx, qword ptr [rbp+rbx*4]
	ror	rdx, 32
	mov	qword ptr [rsi+REGISTER+rax*4], rdx
	ret 

	%else

	mov	ecx, dword ptr [rbp+rbx*4]
	mov	dword ptr [rsi+rax*4], ecx
	mov	ecx, dword ptr [rbp+rbx*4+4]
	mov	dword ptr [rsi+rax*4++4], ecx
	ret

	%endif

;	esi -> core.REGISTER

_operand_write:
	test	eax, 00FFFF00h
	jnz	bus_write

_operand_write_register:
	test	eax, 80h		; in the interrupt registers?
	jz	write1_register
	test	dword ptr [rsi+PSR], 0800000h
	jnz	write1_register
	jmp	guard_ii_authority

write1_register:
	mov	dword ptr [rsi+rax*4], ebx
	ret

bus_write:				; write any memory
					;	eax <- EA
					;	ebx <- data | [ ebp + ebx * 4] -> 1st data word
					;	cl  <- encoding [ word * 1 / 2 / 4 ] / byte
					;	esi <- core

	call	base_write_pointer	; returns eax <- block
					;         edi <- EA
					;	  esi <- word offset

	test	eax, 00400000h		; big block?
	jz	memory_write_4k_block	; no
	cmp	cl, 6			; multiword operation?
	jb	bus_write_256k_block	; no

	cmp	esi, 03FFFCh		; near the edge?
	jna	bus_write_256k_block	; no

	call	write_straddle_18b?

bus_write_256k_block:
	and	eax, 003FFFFFh		; 64-bit shift to page size won't rub off 00400000 bit
	test	eax, 63			; a device array?
	jz	memory_write_page	; no. scaling shift will scrub bit 400000
	jmp	_device_write		; yes a device array

memory_write_4k_block:
	test	esi, 0003F000h		; inside a 4k page?
	jz	memory_write_thru
	mov	ecx, LP_ADDRESS
	jmp	guard_ii_escape		; no

memory_write_thru:
	cmp	cl, 6			; multiword operation?
	jb	memory_write_page	; no

	cmp	esi, 0FFCh		; near the edge?
	jna	memory_write_page	; no

	call	write_straddle_12b?

memory_write_page:
	movq	rdi, mm7
;	mov	rdi, qword ptr [rbp+CORE_INDEX1]

	cmp	eax, dword ptr [rdi+IO_PORT+_RAM_THRESHOLD] ; edi -> core registers
	js	near guard_ii_authority	; guard the NVRAM whether real or emulated

	shl	rax, 12			; multiply by page and lose bit 00400000
	add	rsi, rax		; add offset

;	mov	ch, byte ptr [edi+PSR]	; field of PSR with byte options
	mov	rdi, _memory

memory_write_on:
	lea	rdi, [rdi+rsi*4]

	movq	rsi, mm7
;	mov	rsi, qword ptr [ebp+CORE_INDEX1]

	%if	0
	and	r15, r15		; is there any touchpoint?
	jz	memory_write_on_go
	cmp	rdi, r15		; value of TOUCHPOINT
	js	memory_write_on_go	; below range
	cmp	rdi, qword ptr [_touchpoint2]
	jns	memory_write_on_go	; above range
	jmp	guard_ii_authority

	%endif

memory_write_on_go:
	movzx	rax, cl
	mov	rax, qword ptr [write_action+rax*8]
	jmp	rax

write_action:
	dq	memory_write
	dq	write_t1
	dq	write_t2	; or h1
	dq	write_t3	; or h2
	dq	just_dont
	dq	just_dont
	dq	memory_write2
	dq	memory_write4

memory_write:					; store system memory 1 word

	%if	__MOVBE

	movbe	dword ptr [rdi], ebx

	%else

	bswap	ebx
	mov	dword ptr [rdi], ebx

	%endif

just_dont:
	ret

write_t1:
	mov	byte ptr [rdi+1], bl
	ret

write_t2:
	test	byte ptr [rsi+PSR], HALF_W
	jnz	write_h1
	mov	byte ptr [rdi+2], bl
	ret

write_t3:
	test	byte ptr [rsi+PSR], HALF_W
	jnz	write_h2
	mov	byte ptr [rdi+3], bl
	ret

write_h1:
	%if	__MOVBE
	movbe	eax, dword ptr [rdi]
	%else
	mov	eax, dword ptr [rdi]
	bswap	eax
	%endif

	and	ebx, 4095
	shl	ebx, 12
	and	eax, 4095
	or	eax, ebx
	jmp	write_hw

write_h2:
	%if	__MOVBE
	movbe	eax, dword ptr [rdi]
	%else
	mov	eax, dword ptr [rdi]
	bswap	eax
	%endif

	shr	eax, 12
	shl	eax, 12
	and	ebx, 4095
	or	eax, ebx

write_hw:
	%if	__MOVBE
	movbe	dword ptr [rdi], eax
	ret

	%else

	bswap	eax
	mov	dword ptr [rdi], eax
	ret

	%endif

	%if	__MOVBE

memory_write2:
	movbe	qword ptr [rdi], rax
	ret	

memory_write4:
	movbe	qword ptr [rdi+2*4], rax
	movbe	qword ptr [rdi], rdx
	ret

	%else

					; store system memory 2 words
memory_write2:				; eax is already free
	mov	eax, dword ptr [rbp+rbx*4]
	bswap	eax
	mov	dword ptr [rdi], eax

	mov	eax, dword ptr [rbp+rbx*4+4]
	bswap	eax
	mov	dword ptr [rdi+4], eax
	ret

memory_write4:					; store system memory 4 words
	mov	eax, dword ptr [rbp+rbx*4]	; bx is a register number and may
						; identify extra workspace registers
						; at register stack tail
	bswap	eax
	mov	dword ptr [rdi], eax
	mov	eax, dword ptr [rbp+rbx*4+4]
	bswap	eax
	mov	dword ptr [rdi+4], eax
	mov	eax, dword ptr [rbp+rbx*4+8]
	bswap	eax
	mov	dword ptr [rdi+2*4], eax
	mov	eax, dword ptr [rbp+rbx*4+12]
	bswap	eax
	mov	dword ptr [rdi+3*4], eax
	ret

;	modify instructions n inc dec src slc sim popA
;	rewrite here

	%endif

memoreg_writeback:
	and	rdi, rdi
	jnz	memory_direct
	and	eax, 00FFFFFFh			; unchanged esi -> core.REGISTER	
	mov	[rsi+REGISTER+rbx*4], eax	; platform endian
						; register writeback index ebx ->
	ret

memory_direct:
	%if	__MOVBE
	movbe	dword ptr [rdi+rsi*4], eax
	%else
	bswap	eax				; write components edi esi kept from base_read
	xor	al, al				
	mov	dword ptr [rdi+rsi*4], eax
	%endif
	ret


;	execute calls memory read
;	ea < 256 is not for execute a register
;	operand is at a readable address in system memory or array

memory_read:					; for execute and modify instructions:
	call	memory_point			; read only executable space
	%if	__MOVBE
	movbe	eax, dword ptr [rdi+rsi*4]
	%else
	mov	eax, dword ptr [rdi+rsi*4]	; and similar memory types
	mov	al, 0
	bswap	eax
	%endif
	ret

;	modify instructions n inc dec src slc sim popA call here
;	ea < 256 is a register

memoreg:					; read-modify-write registers / system memory
	test	eax, -256			; but nothing marked as a peripheral bus
	jnz	memory_read_lock
	test	al, 128				; interrupt register?
	jz	register_read_for_write		; no, clear to update
	test	byte ptr [rsi+PSR+2], 128	; ISR ?
	jnz	register_read_for_write
	jmp	guard_ii_authority

register_read_for_write:			; unchanged esi -> core.REGISTER
        mov     ebx, eax                        ; copy of ea -> registers to memoreg_writeback
        xor     rdi, rdi                        ; tell memoreg->writeback that's how it is, no device
        mov     eax, dword ptr [rsi+REGISTER+rax*4]
        ret					; register writeback index ebx ->


;	RAM boundary in port 124 defines the upper page limit
;	of a real or emulated NVRAM containing the fixed system image

;	fixed system image may be kernel + loader in a dozen pages
;	or it may be the entire application

;	emulated machine applies the update limit
;	whether NVRAM is emulated or real

;	if not a register
;	n inc dec src slc sim popA read a memory word which is legal to write
;	and keep the platform address to write the modified value
;	modify instructions do not lock memory between read and rewrite

memory_read_lock:

	call	memory_lock
	%if	__MOVBE
	movbe	eax, dword ptr [rdi+rsi*4]
	%else
	mov	eax, dword ptr [rdi+rsi*4]
	mov	al, 0
	bswap	eax
	%endif
	ret

;	memory_read_lock calls here for modify instructions if ea > registers
;	ts calls here with ea whatever range always -> memory

;	ea must be a legally writable memory word
;	memory_lock does not lock but identifies a platform write address  
;	ts reads / unconditionally replaces word.t1
;	under platform locking protocol, xchg platform instruction

memory_lock:					; point for update
						; barred below RAM boundary as write is
	call	base_write_pointer		; resolve device:block:offset from EA
						; edi <- saved EA not used here
						; eax <- memory block index
						; esi <- memory word offset
	movq	rdi, mm7
;	mov	rdi, qword ptr [rbp+CORE_INDEX1]

	test	eax, 00C00000h			; update block name is a big bank ?
	jz	memory_lock_page		; no

;	and	eax, 003FFFFFh			; 64-bit shift won't rub off 00400000 bit
						; on shift to page size

	test	eax, 63				; a device outside executable space ?
	jnz	_yspace_read			; yes
	and	eax, 003FFFFFh			; bit 0400000 is gets stripped later anyway
	cmp	eax, dword ptr [rdi+IO_PORT+_RAM_THRESHOLD]	; below RAM boundary?
	jnb	_xspace_read			; no, go ahead
	jmp	guard_ii_authority

memory_lock_page:
	cmp	eax, dword ptr [rdi+IO_PORT+_RAM_THRESHOLD]	; below RAM boundary?
	jnb	_xspace_read4K			; no, go head if offset < 1000 hex
	jmp	guard_ii_authority


;	memory_read calls here for execute
;	identify a platform read address of an RTA memory word

memory_point:
	call	base_read_pointer		; edi <- saved EA not used here
						; esi <- word offset in memory block
	test	eax, 00C00000h			; eax <- page index
	jz	_xspace_read4K

;	and	eax, 003FFFFFh			; 64-bit shift won't rub off 00400000 bit
						; on shift to page size
	test	eax, 63
	jnz	_yspace_read			; device correct type or not
	and	eax, 003FFFFFh
	jmp	_xspace_read			; 262144-wprd page of system memory

_xspace_read4K:
	test	esi, 003F000h
	jz	_xspace_read
	mov	ecx, LP_ADDRESS
	jmp	guard_ii_escape

_xspace_read:					; must be a read in a sysmem-like device
	mov	rdi, _memory
	jmp	_memory_deliver			; device array zero

_yspace_read:
	test	eax, 00800000h			; a tripsy non-device?
	jnz	_zspace_read			; not allowed

	shl	rax, 2
	movzx	rbx, al				; rbx <- device bits * 4
;	and	bl, 63
	movq	rdi, mm7			; read out device descriptor
	mov	ebx, dword ptr [rdi+IO_PORT+_DEVICE+rbx]

	test	ebx, SYSMEM_FLAG		; must be	800000 sysmem	aligned array
	jz	_zspace_read
	test	ebx, DATA16_FLAG		; may not be	C00000 filestore packed array
	jnz	_zspace_read			; may not be	400000 16-bit	network trunk

;	shl	rax, 2				; al = 4 * device index
	movzx	rbx, al				; rbx = device bits * 4
						; *2 gives *8
	mov	rdi, qword ptr [_devices+rbx*2]	; _devices are 64-bit pointers
;	xor	al, al				; necessary to lose the device bits now
	shr	eax, 2				; eax = bank * 64 = page in 28 bits

	and	eax, 003FFFC0h			; loose 400000 bit and device bits

_memory_deliver:
	shl	rax, 12				; multiply by page
	add	rsi, rax			; bit 00400000 aleady removed
						; end of memory_point memory_lock
	%if	0
	%if	__SMP
	mov	eax, dword ptr [rdi+rsi*4]
	bswap	eax
	%else
	mov	eax, dword ptr [rdi+rsi*4]
	bswap	eax
	%endif
	%endif

	ret

_zspace_read:
	jmp	guard_ii_authority


;	sub-architectural measure on sabr and reload instructions

;	check the target memory block is in range and the bus characteristic
;	of the memory device when selecting blocks of memory, not when
;	accessing them. Use 8 spare bits in the word containing _base[]
;	for a quick recall of the emulated bus attribute on reference

;	because this software emulation needs to act differently for

;		executable space	rta1 24-bit words canonical memory
;					modeled in platform 32-bit words

;		shared network buffers	16-bit words canonical in memory
;					and zero extend on readout to RTA1

;		24-bit filestore array	3-octet words stored big-endian
;					in packed array

;	concept RTA1 device arrays with bus behaviour differences are completely
;	emulated on PC emulations because device arrays are modeled in PC RAM.

;	Various bus behaviours are emulated transparently to RTA1 target software
;	in anticipation	that different memories for different uses may be attached
;	on product boards, making core-emulated RTA1s much more efficient for not
;	needing this part of emulation

;	devices

;	device 0 is always executable space, but there may be more devices
;	with bus characteristics like device 0. The device descriptors are

;		_________________________________________________________________
;		| 16 bits flags	| 16 bits banks	|       32 bits pointer		|
;		|_______________|____high index_|_______________________________|


;	[out]	sabr	[go]	[call]	reload instructions call here
;	go and call can probably check quicker themself
;	out can be trusted because applications don't out memory _base[] ports

;	a big block in an RTA1 address window is 262144 words.
;	this checking concerns address windows tagged 2..63
;	a device is an array up to 65536 big blocks or banks

;	you get here with the relocation handle in ebx
;	and the target window tag in eax, because that is the EA of a sabr instruction

device_read:				; readout device arrays
        test    eax, 0800000h           ; the vilainous bank index?
        jnz     device_read_escape

					; edi -> core.REGISTER
	shl	rax, 2			; al is device index * 4
	push	rax			; rest of eax is block index * 4
	movzx	rax, al			; extend device number
					; sense device descriptor
	movq	rdi, mm7
;	mov	rdi, qword ptr [rbp+CORE_INDEX1]
	test	byte ptr [rdi+rax+IO_PORT+_DEVICE+2], 192

	pop	rax			; recover storage block index

	jz	device_read_escape	; neither type flag set

	pushfq				; sign is set for descriptor bit 23	
					; parity is set if both bits are

;	mov	ch, byte ptr [rdi+PSR]	; flag field with byte options for memory read
	movzx	rdi, al			; device select

	xor	al, al			; add storage block to offset
	shl	eax, 8			; brush of bit 00400000 (now bit 01000000)
	shl	rax, 10-8		; block index * 64 * 4 now times 262144
	add	rsi, rax		; offset += <-


	mov	rdi,  qword ptr [_devices+rdi*2]	; rdi = device id * 4
							; pointers in array are * 8 bytes

					; known that one or two bits is set
	pop	rax			; flags from test of device	

	test	al, 4			; parity flag?
	jnz	device_read_array24	; parity -> 2 bits set -> FSYS24 bus

	test	al, 128			; sign -> descriptor bit 23 -> SYSMEM
	jz	device_read_data16	; otherwise NET16 trunk
	jmp	device_read_sysmem	; system memory type if yes				

device_read_escape:
	mov	ecx, LP_ADDRESS
	jmp	guard_ii_escape


device_read_sysmem:			; readout system memory array
	jmp	memory_read_on		; allow all variations partial word / multiword

device_read_data16:			; readout 16-bit array

	cmp	cl, 7
	jz	device_read_data16_4
	cmp	cl, 6
	jz	device_read_data16_2
;	jmp	device_read_data16_1

device_read_data16_1:			; readout 1 word of 16-bit array
	movzx	eax, word ptr [rdi+rsi*2]
	xchg	ah, al
	ret

device_read_data16_2:			; readout 2 words of 16-bit array
	mov	eax, dword ptr [rdi+rsi*2]	; 4.3.2.1
	bswap	eax				; 1.2.3.4
	movzx	rbx, ax				; 0.0.3.4
	shr	eax, 16				; 0.0.1.2
	ret

device_read_data16_4:			; readout 4 words of 16-bit array
	mov	rax, qword ptr [rdi+rsi*2]
	bswap	rax				; 1.2.3.4
	movzx	ecx, ax
	mov	dword ptr [rbp+rbx*4+12], ecx
	shr	rax, 16
	movzx	ecx, ax
	mov	dword ptr [rbp+rbx*4+8], ecx
	shr	rax, 16
	movzx	ecx, ax
	shr	rax, 16
	mov	dword ptr [rbp+rbx*4+4], ecx
	mov	dword ptr [rbp+rbx*4], eax
	ret

device_read_array24:			; readout from packed 24-bit array
	call	device_array24_setpointer

	cmp	cl, 7
	jz	device_read_array24_4
	cmp	cl, 6
	jz	device_read_array24_2
;	jmp	device_read_array24_1

device_read_array24_1:			; readout a word of packed 24-bit array
	movzx	ax, byte ptr [rdi+rsi]
	shl	eax, 8
	mov	al, byte ptr [rdi+rsi+1]
	shl	eax, 8
	mov	al, byte ptr [rdi+rsi+2]
	add	rsi, 3
	ret

device_read_array24_1R:
	movzx	bx, byte ptr [rdi+rsi]
	shl	ebx, 8
	mov	bl, byte ptr [rdi+rsi+1]
	shl	ebx, 8
	mov	bl, byte ptr [rdi+rsi+2]
	add	rsi, 3
	ret

device_read_array24_2:			; readout 2 words of packed 24-bit array
	call	device_read_array24_1
	call	device_read_array24_1R
	ret

device_read_array24_4:			; readout 4 words of packed 24-bit array
	call	device_read_array24_1
	mov	dword ptr [rbp+rbx*4], eax
	call	device_read_array24_1
	mov	dword ptr [rbp+rbx*4+4], eax
	call	device_read_array24_1
	mov	dword ptr [rbp+rbx*4+8], eax
	call	device_read_array24_1
	mov	dword ptr [rbp+rbx*4+12], eax
	ret

_device_write:				; store device arrays
	test	eax, 0800000h		; the vilainous bank index?
	jnz	device_write_escape

                                        ; edi -> core.REGISTER
        shl     rax, 2                  ; al is device index * 4
        push    rax                     ; rest of eax is block index * 4
        movzx   rax, al                 ; extend device number
                                        ; sense device descriptor
	movq	rdi, mm7
;	mov	rdi, qword ptr [rbp+CORE_INDEX1]
        test    byte ptr [rdi+rax+IO_PORT+_DEVICE+2], 192

        pop     rax                     ; recover storage block index

        jz      device_write_escape      ; neither type flag set

        pushfq                          ; sign is set for descriptor bit 23     
                                        ; parity is set if both bits are

        mov     ch, byte ptr [rdi+PSR]  ; flag field with byte options for memory read
        movzx   rdi, al                 ; device select

        xor     al, al                  ; add storage block to offset
	shl	eax, 8			; brush off bit 00400000 (now bit 01000000)
        shl     rax, 10-8		; block index * 64 * 4 now times 262144
        add     rsi, rax                ; offset += <-

        mov     rdi,  qword ptr [_devices+rdi*2]	; rdi = device id * 4
							; pointers in array are * 8 octets

                                        ; known that one or two bits is set
        pop     rax                     ; flags from test of device     

        test    al, 4                   ; parity?
        jnz     device_write_array24    ; parity -> 2 bits set -> FSYS24 bus
        test    al, 128                 ; sign -> descriptor bit 23 -> SYSMEM
        jz      device_write_data16     ; otherwise NET16 trunk
        jmp     device_write_sysmem     ; system memory type if yes                     


device_write_escape:
	mov	ecx, LP_ADDRESS
	jmp	guard_ii_escape


device_write_sysmem:
	jmp	memory_write_on		; allow all variations partial word / multiword


device_write_data16:

	cmp	cl, 7
	jz	device_write_data16_4
	cmp	cl, 6
	jz	device_write_data16_2
;	jmp	device_write_data16_1

	%if	__MOVBE

	movbe	word ptr [rdi+rsi*2], bx
	ret

device_write_data16_2:
	mov	ax, word ptr [rbp+rbx*4]
	shl	eax, 16
	mov	ax, word ptr [rbp+rbx*4+4]
	movbe	dword ptr [rdi+rsi*2], ax
	
	ret

device_write_data16_4:
	mov	ax, word ptr [rbp+rbx*4]
	shl	rax, 16
	mov	ax, word ptr [rbp+rbx*4+4]
	shl	rax, 16
	mov	ax, word ptr [rbp+rbx*4+4*2]
	shl	rax, 16
	mov	ax, word ptr [rbp+rbx*4+4*3]
	movbe	qword ptr [rdi+rsi*2], rax
	ret

	%else

device_write_data16_1:			; store 16-bit array
	xchg	bh, bl
	mov	word ptr [rdi+rsi*2], bx
	ret


device_write_data16_4:			; store 4 words in 16-bit array
					; low-order half is swapped on bus read
					; and canonical in eax
	mov	ax, word ptr [rbp+rbx*4+8]	; get low-order half 3rd register
	shl	eax, 16				; 5.6.0.0
	mov	ax, word ptr [rbp+rbx*4+12]	; 5.6.7.8
	bswap	eax				; it's correct so swap for bus write
	mov	dword ptr [rdi+rsi*2+4], eax


device_write_data16_2:			; store 2 words in 16-bit array
					; registers are modeled in storage
					; so each bus read corrects an octet pair

	mov	ax, word ptr [rbp+rbx*4]	; get low-order half 1st register
	shl	eax, 16				; 1.2.0.0
	mov	ax, word ptr [rbp+rbx*4+4]	; 1.2.3.4
	bswap	eax				; it's correct, so swap for bus write
	mov	dword ptr [rdi+rsi*2], eax
	ret

	%endif

device_write_array24:			; store 24-bit packed array
	call	device_array24_setpointer

	cmp	cl, 7
	jz	device_write_array24_4
	cmp	cl, 6
	jz	device_write_array24_2
	jmp	device_write_array24_1

device_write_array24_1:			; store 1 word in packed 24-bit array
	mov	eax, ebx
device_write_array24_x:
	rol	eax, 16
	mov	byte ptr [rdi+rsi], al
	rol	eax, 8
	mov	byte ptr [rdi+rsi+1], al
	rol	eax, 8
	mov	byte ptr [rdi+rsi+2], al
	add	rsi, 3
	ret

device_write_array24_2:			; store 2 words in packed 24-bit array
	mov	eax, [rbp+rbx*4]
	call	device_write_array24_x
	mov	eax, [rbp+rbx*4+4]
	call	device_write_array24_x
;	add	ebx, 2
	ret

device_write_array24_4:			; store 4 words in packed 24-bit array
	call	device_write_array24_2
	add	ebx, 2
	call	device_write_array24_2
	ret

device_array24_setpointer:
	mov	rax, rsi			; think of a number
	shl	rax, 1				; double it
	add	rsi, rax			; add it to the number you first thought of...
	ret

