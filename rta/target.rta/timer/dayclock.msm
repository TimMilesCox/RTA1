dayclock $tree

.	vector name dayclock_ydsm_gmt

ydsm_gmt
	dl	DAYCLOCK$U
	d	1000,,xi
	sb	11
	sa	b
	la	6
	j	ydsm_second

.	vector name dayclock_ydsm

ydsm	dl	DAYCLOCK$U
	d	1000,,xi
	sb	11
	sa	7
	inA	time$zone	. seconds difference from GMT
	dsa	24
	da	6

ydsm_second
	d	(SECONDS_DAY)
	sb	10
	sa	b
	la	6
	da	(DAYS_B4_POSIX:d)
	z	7		. not a leap year so far
	d	(DAYS_400Y)
	sb	9
	lb	a
	m	400,,xi
	sb	8		. only 1 product word
	la	9		. days are a remainder so zero-relative
	aa	-366,,xi	. days this year are 0..365
	jc	century?	. 366+ = 376+ days: not quadcentury

	n	7		. quadcentury + leap year
	j	msdy

century?
	la	9
	dsr	24
	d	(DAYS_100Y)
	sb	9
	lb	a
	m	100,,xi
	ab	8
	sb	8

	la	9
	aa	-365,,xi
	jnc	msdy		. century, leapyear remains off
				. zero-relative remainder
				. days are 0..364 this year

				. 365+ = 366+ days
				. not a century and not leap year
	la	9		. is it a quadyear?
	dsr	24
	d	DAYS_4Y,,xi

	sal	2		. quotient * 4 years
	aa	8
	ds	8

	ab	-366,,xi	. 366+ remainder = 367+ days
	jc	yearin4?

	n	7		. leap year
	j	msdy

yearin4?
	la	9
	dsr	24
	d	365,,xi
	aa	8
	ds	8

msdy	fret	0		. ydms backwards dontcha know?

	$root
