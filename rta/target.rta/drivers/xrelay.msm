.       
.    Copyright Tim Cox, 2012
.    TimMilesCox@gmx.ch
.
.    This file is instruction code for the freeware processor architecture
.
.                RTA1
.
.    RTA1 is a free processor architecture specification.
.
.    The executable emulation of RTA1 is free software.
.
.    This instruction code for the target RTA1 architecture is free software
.
.    Software programs delivered with this software to connect the
.    emulated RTA1 with real network interfaces in the emulator host
.    are free software
.
.    Scripts and programs delivered with this software for running
.    on other computers and interacting with the RTA1 are free software
.
.    Scripts and utility programs for constructing RTA1 target
.    executable software are free software      
.
.    You can redistribute it and/or modify RTA1
.    under the terms of the GNU General Public License as published by
.    the Free Software Foundation, either version 3 of the License, or
.    (at your option) any later version.
.
.    RTA1 is distributed in the hope that it will be useful,
.    but WITHOUT ANY WARRANTY; without even the implied warranty of
.    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.    GNU General Public License for more details.
.
.    You should have received a copy of the GNU General Public License
.    along with RTA1.  If not, see <http://www.gnu.org/licenses/>.
.
.

IFT1_RX_BASE	$equ	32
IFT1_TX_BASE	$equ	32

IFT1_RPAGEW	$equ	1024
IFT1_TPAGEW	$equ	1024

IFT1_RPAGES	$equ	128
IFT1_TPAGES	$equ	128


$(67::, y)
networkframe	delivery


$(0)

ift1_rxtx
	la	this_if:ift1:base_rx
	ii	XBASE$32
	ly	this_if:ift1:rx_cursor
	
frames?	dl	networkframe:flag	. 2nd word in B is total frame length
				. this may include the length of
				. a 2-byte protocol family tag
				. appended to the front of a dgram
				. which has no MAC header

	and	(008000)

	jza	next_interface	. capture not complete

	la	networkframe:ll_hl	. link layer header length

	MIB_ACOUNT48	this_if:inoctets	b
	MIB_ICOUNT48	this_if:inpackets

	sy	fp
	ly	networkframe:frame,,i	. offset descriptor->frame

	anb	a		. net datagram length
	jnc	frame_complete	. less than llh length!
	lr	b		. datagram length bytes

				. the protocol family tag
				. is mostly the word
				. before the datagram

				. either because there is
				. a DIX MAC header, or
				. a similar encapsulation
	la	1,a,i		. round up
	sar	1		. byte count to word count
				. only two bytes per word
				. in packet data

				. MAC header / TAG must
				. be even # bytes

	ay	a

	la	networkframe:protocol, fp		. load the protocol TAG


	push	fp
	push	x

	lx	networkframe:interface, fp	. whatever sort of frame this is
	ax	$if2p,,xi
	lx	0, x			. it goes to this logical interface

	call	(frame_to_protocol)

	dpop	x

frame_complete
	z	networkframe:flag
	ay, xi	RPAGEW
	la	y


	$if	RPAGEW*RPAGES>262144

	ana	(RX_BASE+1*/18:s)
	jnc	frames?
	ly	(RX_BASE*/18++RPAGEW*0:s)

	la	base_rx
	aa, xi	64

	lk	(03FFFC0)
	mta	(RPAGEW*RPAGES/*18-6:s)
	j	$+2
	and	(0C0003F)
	sa	base_rx
	sabr	RX_BASE
	
	$else

	ana	(RX_BASE*/18++RPAGEW*RPAGES:s)
	jnc	frames?
	ly	(RX_BASE*/18++RPAGEW*0:s)

	$endif

	j	frames?
	
next_interface
	sy	this_if:ift1:rx_cursor


txq_ift1_relay
	dl	this_if:ift1:base_tx, x
	ii	XBASE$32
	la	this_if:mtu, x
	sa	MTU

	sb	fp

txq_ift1_otraffic?		
	tz	networkframe:flag, fp	. interface buffer available
	j	txq_next_interface	. not yet, next device array


	lcal	readifq			. frames in this queue are in
	jza	txq_next_queue		. default socket buffer pool
	push	a			. completely detach descriptor from queue before
					. dualport_write clears frame-gather microstac
	lcal	txq_ift1_oframe
	lcal	txq_ift1_next_frame

	pop	a			. recover descriptor

	tp	descriptor:bytes, a
	j	txq_ift1_otraffic?

	push	x
	lx	tx_buffers
	call	(if_sell1)
	pop	x

	j	txq_ift1_otraffic?

txq_next_queue

	$if	HVSOCKET

	$if	1
	lcal	rqtxL
	jza	txq_next_interface
	push	a
	lcal	txq_ift1_oframe
	lcal	txq_ift1_next_frame
	pop	a
	tp	descriptor:bytes, a	. protocol wants to keep buffer?
	j	txq_next_if_space?	. yes
	push	x
	lx	*(BSOCKET*/12++tlist:txbu_p)
	call	(if_sell1)
	pop	x

txq_next_if_space?
	tz	networkframe:flag, fp
	j	txq_next_interface
	j	txq_next_queue

	$else

	la	this_if:portP		. large connections table output chain head
	jza	txq_next_interface

txq_block
	sabr	BSOCKET
	ly	(BSOCKET*/12++tlist:connection_block(1)+transmission:q4ack)

txq_next_frame?
	la	xmit_list.head
	jza	txq_no_waiting_frames

	lb	descriptor:ifql, a	. completely separated from queue before
	sb	xmit_list.head		. dualport_write clears frame-gather microstac

	dpush	y			. [ y : a ]
	lcal	txq_ift1_oframe		. 1st time thru there is an I/F trunk buffer
	lcal	txq_ift1_next_frame
	dpop	y			. [ y . a ]

	tp	descriptor:bytes, a	. TCP only so far
	j	txq_ift1_next_frame_slot?

	dpush	x			. but let's plan ahead
	lx	*(BSOCKET*/12++tlist:txbu_p)
	call	(if_sell1)
	dpop	x

txq_ift1_next_frame_slot?
	tz	networkframe:flag, fp
	j	txq_next_interface

	j	txq_next_frame?

txq_no_waiting_frames
	la	*(BSOCKET*/12++tlist:txchain)	. next connections buffer
	z	*(BSOCKET*/12++tlist:txchain)	. out of chain
	sa	this_if:portP			. next = new chain head
	jnza	txq_block			. if there is any
	z	this_if:portQ			. there is no last because that is
	$endif					. a test of being already in the chain
	$endif
txq_next_interface

	fret	0

mplex_tx				. just in case
	fret	0			. a call is made by mistake


txq_ift1_oframe
	$if	0
	push	x			. save interface pointer a moment
	lx, i	networkframe:frame, fp	. point x to frame buffer after control header
	$endif
				
	lb	descriptor:i_f, a	. translate RTA  interface number to osserv interface number

	ab	-2,,xi			. xlate RTA1 i_f:id to trunk i_f:i_d in frame descriptor
	jpb	txq_ift1_oframe_ahead	. ift2 sometimes loses this id. mark it for repeat route / send
	$do	0,	ii	GUARD$
	lb	descriptor:frame, a	. mark it transmitted
	z	nframe:stack_index, b	. so it may be retransmitted
	lret	1			. skip call to update interface buffer pointer

txq_ift1_oframe_ahead 
	sb	networkframe:interface, fp

	la	descriptor:frame, a	. transfer protocol tag to interface buffer header

	push	nframe:frame_axw(1):bytes, a
	pop	networkframe:ll_hl, fp

	push	nframe:llh:generic:protocol, a
	pop	networkframe:protocol, fp	. leaving frame pointer in a

	push	x				. save interface pointer
	lx	networkframe:frame, fp, i	. write pointer in interface slot
	call	(if_dualport_txwrite)
	pop	x				. retrieve  interface pointer

	$if	0
	lb	networkframe:protocol, fp
	ab	-0800,,xi
	jnzb	not_ip

        push    networkframe:frame, fp, i
        push    this_if:llhl
        off     carry$
        src     0, sp
        popA    1, sp

	c	(__recheck)	0, sp
	inc	sp
not_ip

	$endif

	push	TX_DATAGRAML		. release interface buffer for transmission
	pop	networkframe:frame_length, fp
	push	(008000)
	pop	networkframe:flag, fp

        MIB_ACOUNT48    this_if:outoctets       TX_DATAGRAML
        MIB_ICOUNT48    this_if:outpackets

txq_ift1_2x
	lret	0

	$if	0
txq_oframe_bypass
	pop	x			. inelegant but safe. ift2 frame has lost target i_f id
	z	nframe:stack_index, a	. clear xmit staclet = qualify it for repeat route + retransmit if tcp
					. calling routine already decoupled it from I / F queue
					. note it is no use copying i_f:id from interface structure
					. in ift1 that is always logical interface 2
					. multiplexing all attached physical interfaces. For emulated RTA1
					. copying i_f(2) identity indicates platform loopback to the trunk
	$do	0,	ii	GUARD$	. change the 0 to 1 to get a diagnostic halt
	push	-1,,xi
	popA	this_if:outdrops+1	. log it to i_f(2) because interface identity is the problem
	jnc	$+2			. unless this->i_f is pointing to the logical ift2 interface
	inc	this_if:outdrops	. where this frame is intended, which is even better
	lret	0
	$endif

txq_ift1_next_frame
	lb, xi	IFT1_TPAGEW
	ab	fp
	andB	(IFT1_TPAGES*IFT1_TPAGEW-IFT1_TPAGEW:s)
	orB	(IFT1_TX_BASE*/18++IFT1_RPAGES*IFT1_RPAGEW:s)
	
	sb	fp
	sb	this_if:ift1:tx_cursor, x
	lret	0

rxq_ift1_next_frame
	ly, xi	IFT1_RPAGEW
	ay	fp
	lb	y
	anb	(IFT1_RX_BASE*/18++IFT1_RPAGES*IFT1_RPAGEW:s)
	jnc	$+2
	ly	(IFT1_RX_BASE*/18:s)
	sy	fp
	sy	this_if:ift1:rx_cursor, x
	lret	0

start_ift1
	sa	this_if:flags
	lb	(0400000)
	orB	y
	sb	this_if:ift1:base_rx
	sb	this_if:ift1:base_tx
	fret	0

.	called only on system restart
.	so no settings are saved
.	requests external interface service to restart
.	to match automatic resynchronisation at this end

.	it's the only time a CONFIGURATION_PROTOCOL frame is output
.	so it doesn't need much detail

p	$proc
restart_frame* $name
	qs	1, x
	sk	0, x
	ax	0400,,xi
	$end

restart_xrelay_interface
	inA	66				. kernel vector list
	jza	restart_emergency		. system is not configured
	jna	restart_emergency		. or has got deconfigured

restart_from_configuration			. system appears to be configured
						. soft restart attempt
	on	see$2				. IP vector list
	la	VECTOR_B1(1)			. IP B1 1st frame
	outA	1
	off	see$1				. make sure 1 and not 65
	lx	01002				. ifxpointer(2)

	dl	this_if:ift1:base_tx, x		. pick up the network trunk position
						. and send restart requests from there
restart_xrelay
	jnza	restart_base_configured
	la	(0400002)			. must be before interface configuration

restart_base_configured
	sabr	32				. this is already interrupt code
	lx	b_x				. interrupt code, remember?

	ql	(0, 0, 0, IFCONFIG:q)
	lk	(008000)

	lr	5,,xi
	sr	8+128				. scratch register counter
	lr	(33*/18s)
	j	restart_frame_xmit

restart_bank
	lx	(32*/18++131072s)
restart_frame_xmit
	jxge	restart_bank
	restart_frame
	
	dec	8+128
	tz	8+128
	j	restart_frame_xmit
	
	fret	0


restart_emergency				. trouble before initialisation
	la	(0400002)			. but we know really anyway
	sabr	32
	lx	(32*/18++131072s)
	ql	(0, 0, 0, IFCONFIG:q)
	lk	(008000)

	lr	5-1,,xi

restart_base_frame				. pump out some reconfigure frames
	restart_frame				. at the start location of network trunk
	jdr	restart_base_frame

.	having done that it's likely the startup fault will fault again on restart
.	so try to pull an intervention interface up in single step (LOCKSTEP)

.	if that isn't featured do a 5-second processor sleep

	dl	(CHILLDOWN++LOCKSTEP, 5000000d)
	outB	chill$down
	outA	INDICATION$

	fret	0

