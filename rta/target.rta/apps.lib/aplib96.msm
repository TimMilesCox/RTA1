
ap_lib96 $tree

.	vector name fraction96

fraction $tree
	$head_far	 params(input,	float	)	;
			scalars(i_rkxy,	float,	r	)

	ql	input
	lx	a
	tp	x		. obtain magnitude exponent
	n	x
	ax	(-0400000)	. obtain relation to midpoint
	tz	x
	tp	x
	j	all_fraction	. no integer part, don't tamper
	lr	72,,xi
	jxge	all_integer	. no fraction part, zero the fraction

	c	(integer96)	input,,float
	n	a
	n	b
	n	6
	n	7		. subtract the integer part
	fa	input		. fromm the fraction
	j	complete

all_integer
	dsa	48
	ds	6

all_fraction
complete
	qpop	r
	fret	0

	$root


.	vector name integer96

integer	$tree
	$head_far	 params(input,	float	)	;	. pointer rto float
			scalars(i_rkxy,	float,	r	)

	ql	input
	lx	a
	tp	x		. obtain magnitude exponent
	n	x
	ax	(-0400000)	. obtain relation to midpoint
	tz	x
	tp	x
	j	all_fraction	. no integer part, zero the integer
	lr	72,,xi
	jxge	all_integer	. no fraction part, don't tamper

	lcal	ap_lib96:sign_around

	j	complete

all_fraction
	dsa	48
	ds	6

all_integer
complete
	qpop	r
	fret	0
	$root

sign_around					. write signs to mantissa bits [x]+
	$head_near	scalars(output,	float,	a)

	saa	24

	lr	48,,xi
	jxge	insert7
	lr	24,,xi
	jxge	insert6

	sa	output+3
	sa	output+2
	lb	output+1
	drl	0, x
	rar	0, x
	sa	output+1
	j	reload

insert6	sa	output+3
	lb	output+2
	ax	-24,,xi
	drl	0, x
	rar	0, x
	sa	output+2
	j	reload

insert7	lb	output+3
	ax	-48,,xi
	drl	0, x
	rar	0, x
	sa	output+3
	
reload	qpop	a
	lret	0

	$root
